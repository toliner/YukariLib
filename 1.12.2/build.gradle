buildscript {
    ext.kotlin_version = '1.3.30'
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'

version = modVersion
group = modGroup
archivesBaseName = modBaseName

tasks.withType(Jar) {
    compileJava.options.encoding = 'UTF-8'
    compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileKotlin{
    kotlinOptions{
        jvmTarget = '1.8'
    }
}

sourceCompatibility = targetCompatibility = "1.8"

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    mappings = "snapshot_20171003"
    makeObfSourceJar = false
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    embed "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}